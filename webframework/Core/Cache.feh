global {
   object currentPage;
}

uses "console";

namespace Cache {
	
	string cacheExtension = ".cache";
   
	function generateCacheFilePath( string path ) {
		string pagePath = path;
		pagePath = Regexp.replace('^.*?([A-Za-z]+).app', path, '\1.app');
		pagePath = Regexp.replace('^/', pagePath - (Config.PublicPath - "Server/"), '');
		return Config.CachePath + Regexp.replaceAll( "/", pagePath + .cacheExtension, "." );
	}

	function loadPageComponents( string page ) {
		string serialComponentList = "";
		string cache_path = .generateCacheFilePath(pathForPageTemplate(page));
		string component_list_cache_path = cache_path + ".components.list";
		string active_component_list_cache_path = cache_path + ".components";
		array  componentList = include(component_list_cache_path);
		array  activeComponentList;

		componentList.each() using ( name ) { 
			GUIKit.loadComponent(name); 
		};
		activeComponentList = Cache.fetch("file:$active_component_list_cache_path", []) using  {
			return Serialize.fromNative(File.open(active_component_list_cache_path).toString());
		};
		if( activeComponentList.keyExists('__STORED_PERSISTENCE_LAYER__') ) {
			Cache.persistence = activeComponentList['__STORED_PERSISTENCE_LAYER__'];
			activeComponentList.del('__STORED_PERSISTENCE_LAYER__');
		}
		Cache.components = [];
		Array.each( activeComponentList ) using ( component ) {
			component.setID(component.id);
			component.initialiseComponent();
		};
	}
   
	function getComponent( string id ) {
		if( Array.keyExists(.components, id) )
			return .components[id];
		return null;
	}
   
	object page;
	array  components;
	object display;
	array  persistence;

	function loadPage( string page ) {
		void rval;
		wf_profile("Cache.loadPage($page)") using {
			string cache_path = .generateCacheFilePath(pathForPageTemplate(page));
			string real_cache_path = cache_path + ".xml";
			string component_list_cache_path = cache_path + ".components.list";
			string component_cache_path = cache_path + ".components";
			string strings_cache_path = cache_path + ".strings.fe";
			string script_cache_path = cache_path + ".fe";
			metrics.checkpoint("Include: $script_cache_path");
			wf_profile("page_inclusion") using {
				rval = include( script_cache_path );
			};
			metrics.checkpoint("Done Include: $script_cache_path");
			metrics.checkpoint("Storing viewstate: ${Cache.persistence}");
			wf_profile("viewstate storage") using {
				ViewState.store(application.action);
			};
		};
		return rval;
	}
   
	function clear( string page ) {
		string cache_path = .generateCacheFilePath(pathForPageTemplate(page));
		string real_cache_path = cache_path + ".xml";
		string component_list_cache_path = cache_path + ".components.list";
		string component_cache_path = cache_path + ".components";
		string strings_cache_path = cache_path + ".strings.fe";
		string script_cache_path = cache_path + ".fe";
		string javascript_cache_path = cache_path + '.javascript.fe';
		
		File.remove(real_cache_path);
		File.remove(component_list_cache_path);
		File.remove(component_cache_path);
		File.remove(strings_cache_path);
		File.remove(script_cache_path);
		File.remove(javascript_cache_path);
	}
	
	function currentPageLastCache() {
		string cache_path = .generateCacheFilePath(pathForPageTemplate(application.action));
		object pagecache_stat = Posix.stat( cache_path + '.xml' );
		if( pagecache_stat )
			return Date.date(pagecache_stat.getModifiedTime()).format(Config.DateFormat + ' %H:%M:%S');
		return I('Not Cached');
	}
	function runEmbfer( string contents ) {
		string file = String.trim(contents," ");
		object o = new Embfer();
		o.setOutputFunction( "__embfer_output += " );
		return o.fromFile(file);
	}
   
	function PreCachePage( string path, string contenttype, void pageclass, object page ) {
		string templateWrapper = pageclass.getWrapperTemplateName();
		string real_path = pathForPageTemplate(path);
		string cache_path = .generateCacheFilePath(real_path);
		string real_cache_path = cache_path + ".xml";
		string component_list_cache_path = cache_path + ".components.list";
		string component_cache_path = cache_path + ".components";
		string strings_cache_path = cache_path + ".strings.fe";
		string script_cache_path = cache_path + ".fe";
		string javascript_cache_path = cache_path + '.javascript.fe';
		string stage = '';

		array  translationCatalog = I18N.catalog;
//		I18N.catalog = [];
		
//		monitor {
			array viewState;
			string serialViewState = "", xmloutput = "", content;
			void renderClass = GUIKit.Render.renderEngines[contenttype];
			object renderEngine = new renderClass();

			object compiler = new Embfer();
			string evaluated_script = "";
			object cachePath = null, file;

			I18N.catalog = [];
			
			if( not (cachePath = Posix.stat(Config.CachePath)) ) {
				raise new Error("Unable to access '${Config.CachePath}', please check the path exists and is writeable");
			}

			stage = 'Loading Page File';
			File.open( real_path ) using ( file ) {
				content = file.toString();
			};

			File.remove(real_cache_path);
			File.remove(component_cache_path);
			File.remove(strings_cache_path);
			File.remove(script_cache_path);
			File.remove(javascript_cache_path);
			
			if( not Embfer.hasTagHandler("include") ) {
				Embfer.registerTagHandler( "include" ) using ( tag, contents ) {
					return Cache.runEmbfer( contents );
				};
			}

			content = renderEngine.wrapContent( content, [ 'Template' => templateWrapper ] ); 

			compiler.setOutputFunction( "__embfer_output += " );
		
			stage = 'Evaluating Cache File:';
			content.toArray("\n").each() using ( line ) {
				string script = "";
				script += "string __embfer_output;";
				script += compiler.fromString(line + "\n");
				script += 'return __embfer_output;';
				evaluated_script += eval(script);
			};

			file = File.create( real_cache_path, 0666 );
			if( not file ) {
				raise new Error("Unable to create cache file '$real_cache_path', please make sure the directory '${Config.CachePath}' is writeable (chmod 777)");
			}
			file.writeln( evaluated_script );

			// Then we cache the components within the page
			stage = 'Loading Cache Template File';
			renderEngine.directLoadTemplate( real_cache_path );
			stage = 'Processing Cache Template File: ';
			renderEngine.processSource();
			page.bindComponents();
			metrics.checkpoint("&rarr; Initialising components");
			page.initialiseComponents();
			page.publishPersistence();
			metrics.checkpoint("&larr; Initialising components");
			
			Cache.components['__STORED_PERSISTENCE_LAYER__'] = Cache.persistence;
			File.create( component_cache_path, 0666 ) using ( file ) {
				file.write( Serialize.toNative( Cache.components ) );
			};
			Cache.components.del('__STORED_PERSISTENCE_LAYER__');
			
			File.create( component_list_cache_path, 0666 ) using ( file ) {
				file.write( 'return [ ' +
						GUIKit.components.join(', ') using ( component ) {
							return "'${component.name}'";
						} + ' ];' );
			};
			// Then we cache the component embodied xml as a ferite script
			stage = 'Generating Page Script';
			xmloutput = renderEngine.render();
			File.create( script_cache_path, 0666 )using ( file ) {
				file.write( xmloutput );
			};
			stage = 'Generating Strings File';
			File.create( strings_cache_path, 0666 ) using ( file ) {
				file.write( renderEngine.generateStrings() );
			};
			stage = 'Generating Javascript Requirment File';
			File.create( javascript_cache_path, 0666 ) using ( file ) {
				file.write( 'return ' + page.getRequiredJavascript() + ';' );
			};
//		} handle (error) {
//			if( webframeworkConsoleTool ) {
//				Console.println("Failed caching at stage $stage: ${err.str}"); 
//			} else {
//				application.failSafeErrorPage( error, "Failure during caching [$stage]", false);
//	
//				if( not Config.Debug ) {
//					File.remove(real_cache_path);
//					File.remove(component_cache_path);
//					File.remove(strings_cache_path);
//					File.remove(script_cache_path);
//					File.remove(javascript_cache_path);
//				}
//				Core.exit();
//			}
//		}
		I18N.catalog = translationCatalog;
	}
	function checkFilesExist( array files ) {
		number count = 0;
		files.each() using ( file ) {
			object stat = Posix.stat(file);
			if( stat ) {
				count++;
			}
		};
		return (count == files.size());
	}
	
	function checkPageCache( string path, string contenttype, void pageclass, object page ) {
		string real_path = pathForPageTemplate(path);
		string cache_path = .generateCacheFilePath(real_path);

		string real_cache_path = cache_path + ".xml";
		string component_list_cache_path = cache_path + ".components.list";
		string component_cache_path = cache_path + ".components";
		string strings_cache_path = cache_path + ".strings.fe";
		string script_cache_path = cache_path + ".fe";
		string javascript_cache_path = cache_path + ".javascript.fe";

		object page_stat = Posix.stat( real_path );
		object pagecache_stat = Posix.stat( real_cache_path );
				
		if( page_stat != null ) {
			boolean forceRecache = false;

			if( Config.Debug ) {
				if( pagecache_stat == null ) {
					forceRecache = true;
				} else {
					/* Check the page templates, if they change, recache */
					void   renderClass = GUIKit.Render.renderEngines[contenttype];
					object renderEngine = new renderClass();
					array  dependants = renderEngine.dependantList( [ 'Template' => pageclass.getWrapperTemplateName() ] ); 
					dependants.each() using ( file ) {
						object stat = Posix.stat(file);
						if( stat.getModifiedTime() > pagecache_stat.getModifiedTime() ) {
							forceRecache = true;
							return false;
						}
					};
				}
			} 
		
			if( forceRecache ||
				not .checkFilesExist([ real_cache_path, component_list_cache_path, component_cache_path, strings_cache_path, script_cache_path, javascript_cache_path ]) ||
				pagecache_stat == null || 
				pagecache_stat.getModifiedTime() < page_stat.getModifiedTime() ) {
					Cache.PreCachePage( path, contenttype, pageclass, page );
					return true;
			}  
		}
		else
		   Sys.error( "Unable to load page '$real_path'\n", 0 );
		return false;
	}

	boolean tryRemote = true;
	array localCache;

	function store( string key, void defaultValue ) {
		return .store(key, defaultValue, 0);
	}
	function store( string key, void defaultValue, number expire ) {
		.localCache[key] = defaultValue;

		if( .tryRemote ) {
			return memcached.serializeSet(key, defaultValue, expire);
		}
		return false;
	}
	function fetch( string key, void defaultValue ) {
		return .fetch( key, defaultValue, 0 ) using recipient();
	}
	function fetch( string key, void defaultValue, number expire ) {
		boolean obtained = false;
		object block = recipient();
		void value;
		
		if( .localCache.keyExists(key) ) {
			wf_profile("Cache.local.$key") using {
				value = .localCache[key];
			};
		}
		else if( .tryRemote ) {
			wf_profile("Cache.fetch.$key") using {
				monitor {
					value = memcached.serializeGet( key );
					obtained = true;
				} handle {
					if( Regexp.match('SYSTEM ERROR', err.str) ) {
						// Disable cache checking if we get system error.
						.tryRemote = false;
					}
					obtained = false;
				}
			};

			if( obtained ) {
				return value;
			}

			wf_profile("Cache.load.$key") using {
				monitor {
					if( block ) {
						value = block.invoke();
						memcached.serializeSet(key, value, expire);
					} else {
						value = defaultValue;
					}
				} handle {
					value = defaultValue;
				}
			};
		} 
		else {
			wf_profile("Cache.ignore.$key") using {
				monitor {
					if( block ) {
						value = block.invoke();
					} else {
						value = defaultValue;
					}
				} handle {
					value = defaultValue;
				}
			};
		}
		
		.localCache[key] = value;
		return value;
	}
	function fetchFile( string path ) {
		return .fetchFile( path, path ) using recipient();
	}
	function fetchFile( string key, string path ) {
		return .fetchFile( key, [ path ] ) using recipient();
	}
	function fetchFile( string key, array paths ) {
		object block = recipient();
		if( not block ) {
			block = closure( input ) { return input; };
		}
		return .fetch( "file:$key", "" ) using {
			string content = '';
			
			paths.each() using ( path ) {
				if( path ) {
					content = File.open(path).toString();
					if( content ) {
						return false;
					}
				}
			};
			
			return block.invoke(content);
		};
	}
	function delete( string match ) {
		if( .tryRemote ) {
			memcached.locate("^$match").each() using ( key ) {
				memcached.default_cache.delete(key);
			};
		}
	}
}
