namespace modifies GUIKit {
	
	boolean isBackgroundEvent = false;

	class Page extends GUIKit.EventHandler {
		
		static string PageID = '__PAGEID__';
		static array PageCache;

		static array ValidSubActions;	   
		directive sub_action( string name ) {
			self.ValidSubActions[name] = true;		   
		}
		static function validSubAction( string name ) {
			return Array.keyExists( self.ValidSubActions, name );
		}

		static function checkStaticVariables() {
			object k = new Class(self);
			array variables = k.getVariables();
			if( not (variables.keyExists('persistenceArray')) ) {
				log('Page.checkStaticVariables', "Creating Static Variables [${Class.name(self)}]");
				eval( "class modifies ${Class.name(self)} { 
							static array persistenceArray; 
							static array persistenceVariables;
							static array linkedObjects;
							static array rpcRequests;
							static array mcamRequests;
							static array mcamChannels;
							static array commandHooks; }" );
			}
		}
		// This is the persistence layer that we can place things within the viewstate
		directive persist( string name ) {
			.checkStaticVariables();
			.persistenceArray[] = name;
		}
		directive persist( string type, string name ) {
			.checkStaticVariables();
			.persistenceArray[] = name;
			.persistenceVariables[name] = type;
		}
		directive component( ... ) {
			.checkStaticVariables();
			Array.each(arguments()) using ( name ) {
				.linkedObjects[name] = true;
			};
		}
		directive rpc_request( string request_name ) {
			object f = new Function(Class.classWithName(Class.name(self)).newObject(), request_name);
			.checkStaticVariables();
			.rpcRequests[request_name] = f.getParameterDetails();
		}
		directive mcam_request( string request_name ) {
			.checkStaticVariables();
			log('Page.mcam_request', request_name + " -> " + Class.name(self));
			.mcamRequests[] = request_name;
		}
		directive mcam_channel( string request_name ) {
			.checkStaticVariables();
			log('Page.mcam_channel', request_name + " -> " + Class.name(self));
			.mcamChannels[] = request_name;
		}
		directive command( string command_name, string function_name ) {
			.checkStaticVariables();
			.commandHooks[command_name] = [ true, function_name ];
		}
		directive hidden_command( string command_name, string function_name ) {
			.checkStaticVariables();
			.commandHooks[command_name] = [ false, function_name ];
		}
	    static function clearBindings() {
			.persistenceVariables = [];
			.persistenceArray = [];
			.linkedObjects = [];
			.rpcRequests = [];
			.mcamRequests = [];
			.mcamChannels = [];
			.commandHooks = [];
		}
		// Get an array of linked objects
		static function linkedObjects() {
			array returnValue;
			
			monitor {
				.linkedObjects.keys().each() using ( component ) {
					returnValue[] = component;
				};
			} handle { 
				returnValue = [];
			}
			monitor {
				array parentLinkedObjects = super.linkedObjects();
				while( parentLinkedObjects )
					returnValue.push(parentLinkedObjects.pop());
			} handle { 
				returnValue = returnValue;
			}
			return returnValue;
		}
		// Get an array of persistant variables
		static function persistentValues() {
			array returnValue;
			
			monitor {
				.persistenceArray.each() using ( variable ) {
					returnValue[] = variable;
				};
			} handle { 
				.returnValue = [];
			}
			monitor {
				array parentLinkedObjects = super.persistentValues();
				while( parentLinkedObjects )
					returnValue.push(parentLinkedObjects.pop());
			} handle { 
				returnValue = returnValue;
			}
			return returnValue;
		}

		array dynamicMCAMHandlers;
		function registerDynamicMCAMHandler( string name, object receiver ) {
			.dynamicMCAMHandlers[name] = receiver;
		}
		function hasHiddenCommands
			return false;

		static constructor {
			string classChanges = '';
			boolean wishToEvaluate = false;
			
			.checkStaticVariables();
			classChanges = "class modifies ${Class.name(self)} { ";
			if( .linkedObjects ) {
				Array.each(.linkedObjects.keys() ) using ( name ) {
					classChanges += "object $name;";
				};
				wishToEvaluate = true;
			}
			if( .persistenceVariables ) {
				Array.map(.persistenceVariables) using ( name, type ) {
					classChanges += "$type $name;";
				};
				wishToEvaluate = true;
			}
			if( .rpcRequests ) {
				classChanges += "function getRPCRequests() {";
				classChanges += "	return ${.rpcRequests};";
				classChanges += "}";
				classChanges += "function handleRPCRequest( string name, array parameters ) {
									switch( name ) {";
				Array.map(.rpcRequests) using ( request_name, request_parameters ) {
					classChanges += "case \"$request_name\":";
					classChanges += "	return SOAP.RPC.response( name, .$request_name(";
					request_parameters.eachWithIndex() using (type, i) {
						string name = request_parameters.getName(i);
						classChanges += "	parameters['$name']" + (i < (request_parameters.size() - 1) ? "," : "");
					};
					classChanges += "	) );";
				};
				classChanges += " default:
									return super.handleRPCRequest( name, parameters );
								} }";
				wishToEvaluate = true;
			}
			if( .mcamChannels or .mcamRequests ) {
				classChanges += "function handleMCAMRequest( string name, array parameters ) { 
									if( .dynamicMCAMHandlers.keyExists(name) ) {
										string result = '' + .dynamicMCAMHandlers[name].handleMCAMCommand( name, parameters );
										if( result ) {
											return result;
										}
									}
									switch( name ) {";
				Array.each(.mcamRequests) using ( request_name ) {
					classChanges += "case \"$request_name\": {";
					classChanges += "	void r = .$request_name( parameters ); 
										return HTML.MCAMChannel('Result', name, '' + r);";
					classChanges += "} ";
				};
				Array.each(.mcamChannels) using ( request_name ) {
					classChanges += "case \"$request_name\": {";
					classChanges += "	return .$request_name( parameters );";
					classChanges += "} ";
				};
				classChanges += " default:
									return super.handleMCAMRequest( name, parameters );
								} }";
				wishToEvaluate = true;
			}
			if( .commandHooks ) {
				boolean hasHidden = false;
				classChanges += "function handleApplicationCommands {";
				classChanges +=	"	object match = null;";
				classChanges += "	if( application.commands ) {";
				Array.map(.commandHooks) using ( command, functionDetails ) {
					object r = new Regexp('\(([A-Za-z0-9]+)(:(string|number|boolean))?\)');
					string functionName = functionDetails[1];
					boolean visible = functionDetails[0];
					array named_captures = [];
					string command_match = r.replaceAll(command) using ( match ) {
						string type = 'string';
						string type_capture = '[A-Za-z0-9 ]+';
						if( match.capture(2) ) {
							type = match.capture(2);
						}
						switch( type ) {
							case 'string':
								type_capture = '[A-Za-z0-9]+';
								break;
							case 'number':
								type_capture = '[+-]?[0-9]+';
								break;
							case 'boolean':
								type_capture = 'true|false';
								break;
						}
						named_captures[] = [ match.capture(0), type ];
						return "($type_capture)";
					};
					command_match = "^$command_match\$";
					classChanges += "	if( (match = Regexp.match('$command_match', application.commands.join('/'))) ) {";
					classChanges += "		void return_value; array values = [];";
					named_captures.eachWithIndex() using ( item, index ) {
						string name = item[0];
						string type = item[1];
						
						switch( type ) {
							case 'string':
								classChanges += "		values['$name'] = match.capture($index);";
								break;
							case 'number':
								classChanges += "		values['$name'] = match.capture($index).toNumber();";
								break;
							case 'string':
								classChanges += "		values['$name'] = (match.capture($index) == 'true' ? true : false);";
								break;
						}
					};
					classChanges += "		return_value = .$functionName( values );";
					classChanges += "		.commandRun = '$functionName';";
					classChanges += "		.hasRunCommand = true;";
					if( not visible ) {
						classChanges += "		.publishPersistence();";
						classChanges += "		Application.performActionWithData(application.action, [ 'webframework_from_hidden_command' => true ]);";
						hasHidden = true;
					} else {
						classChanges += "	if( return_value )";
						classChanges += "		return return_value;";
						classChanges += "	return false;";
					}
					classChanges += "	}";
				};
				classChanges += "		return super.handleApplicationCommands();";
				classChanges += "	}";
				classChanges += "	return false;";
				classChanges += "}";
				classChanges += "function hasHiddenCommands return $hasHidden;";
				wishToEvaluate = true;
			}
			classChanges += " }";
			if( wishToEvaluate ) {
				eval(classChanges);
			}
		}
		
		object renderEngine;
		string action;
		array  commands;
		array  parameters;
		array  validators;	// The validators
		number valid;		// Is the page valid ?
		boolean firstLoad;
		boolean hasRunCommand = false;
		string  commandRun;
		
		function retainViewState
			return GUIKit.isBackgroundEvent;
		function afterHiddenCommand {
			if( Session.hasValueForAction(application.action) ) {
				void value = Session.valueForAction(application.action);
				Session.removeValue(application.action);
				if( value isa array and value.keyExists('webframework_from_hidden_command') and value['webframework_from_hidden_command'] ) {
					return true;
				}
			}
			return false;
		}
		static function load( string page, array commands, array parameters ) {
			void __PageKlass__;
			string cpath = pathForPageCode( page );
			string tpath = page;
			string tmp;
			void potentialPage;

			metrics.checkpoint( "Page.load: loading page class" );
			if( not Array.keyExists( .PageCache, page ) ) {
				if( cpath != "" ) {
					log('Page.load', "include path: $cpath");
					__PageKlass__ = include( cpath );
					tmp = __PageKlass__.getTemplateName();
					if( tmp != "" )
						tpath = tmp;
				}
				else
					__PageKlass__ = GUIKit.Page;
				.PageCache[page] = __PageKlass__;
			} else {
				__PageKlass__ = .PageCache[page];
			}
			metrics.checkpoint("Page.load: done class load: ${__PageKlass__}");
			if( __PageKlass__ != null )
			{
				string contenttype = __PageKlass__.getContentType();
				object pageobj = new __PageKlass__(page);
				object wrapper = new Object(pageobj), component;
				void   renderClass = GUIKit.Render.renderEngineForType(contenttype);
				array  viewstate = [];
				number waterMark = 0;
				array  persistentValues;
				boolean afterHiddenCommand = false;
				
				if( commands and commands[0] == 'MCAM' )
					GUIKit.isBackgroundEvent = true;
					
				currentPage = pageobj;
				Cache.page = pageobj;
				pageobj.setCommandParameters( page, commands, parameters );
				pageobj.firstLoad = false;

				afterHiddenCommand = Cache.page.afterHiddenCommand();
				debug("r: " + request.referer());
				debug("a: " + application.fullAction());
				debug("ui: " + parameters.keyExists('uieventcomponent'));
				debug("hc:" + afterHiddenCommand);
				debug("vs: " + Cache.page.retainViewState());
				if( (request.referer() != '' and Regexp.match("${application.fullAction()}", request.referer())) or parameters.keyExists('uieventcomponent') or afterHiddenCommand or Cache.page.retainViewState() ) {
					debug('allowing viewstate load');
					viewstate = ViewState.fetch(application.action);
				} else {
					debug('not allowing viewstate load');
				}
				if( commands and commands[0] == 'RPC' ) {
					GUIKit.isBackgroundEvent = true;
					monitor {
						object envelope = new SOAP.Envelope(request.queryString(), pageobj.getRPCRequests());
						if( envelope.variables.keyExists('error') ) {
							return SOAP.fault( 'env:Sender', envelope.variables['error']['messages'][0] );
						} else {
							return pageobj.handleRPCRequest( envelope.procedureName(), envelope.procedureParameters() );
						}
					} handle {
						return SOAP.fault( 'env:Receiver', err.str );
					}
				}

				Cache.display = new renderClass();
				pageobj.setRenderEngine( Cache.display );
				metrics.checkpoint("Checking page cache");
				if( Cache.checkPageCache( tpath, contenttype, __PageKlass__, pageobj ) ) {
					metrics.checkpoint("reseting view state");
					viewstate = [];
				}
				metrics.checkpoint("Loading components");
				Cache.loadPageComponents( tpath );

				if( Array.size(Cache.components) > 0 ) {
					void  actionValue;
					void command_return;
					
					pageobj.bindComponents();
					
					component = pageobj.objectForID('PageTitle');
					if( component and component.textValue == '' )
						component.setTextValue( application.virtualName + ' - ' + page );
					component = pageobj.objectForID('PageUsername');
					if( component ) {
						object u = Permissions.fetchUserObject();
						component.setTextValue( (u ? u.toName() : 'Guest') );
					}
					component = pageobj.objectForID('PageCurrentDate');
					if( component )
						component.setTextValue( I('Current Date: {0:date string}').expand( formatDate(Date.now()) ) );

					actionValue = Session.valueForAction(application.action);
					metrics.checkpoint("$parameters");

					Array.map( Cache.persistence ) using ( string key, void value ) {
						metrics.checkpoint("Restoring $key: $value");
						wrapper.setVariable( key, value );
					};
					
					pageobj.firstLoad = not (parameters.keyExists('uieventcomponent') or GUIKit.isBackgroundEvent);
					if( parameters.keyExists('uieventcomponent') or GUIKit.isBackgroundEvent or viewstate ) {
						// Event processing
						object actionObject = null;
						
						log('Page.load', "Got second load");
						metrics.checkpoint("checking static variables");
						.checkStaticVariables();
						metrics.checkpoint("view state keys: ${viewstate.keys()} ${viewstate.size()}");
						persistentValues = Cache.display.processViewState( viewstate );
						Array.map( persistentValues ) using ( string key, void value ) {
							metrics.checkpoint("Restoring $key: $value");
							wrapper.setVariable( key, value );
						};

						// We need to go through the variables and update their properties
						Array.each( parameters.keys() ) using ( obj ) {
							if( Cache.components.keyExists(obj) )
								Cache.components[obj].handleFormValue( parameters[obj] );
						};

						// Validate
						if( pageobj.processValidators() ) {
							if( not (command_return = pageobj.handleApplicationCommands()) ) {
								// Notify the page object that is has been loaded
								metrics.checkpoint("&rarr; Running invokePageLoaded()");
								wf_profile("pageLoaded:event") using {
									potentialPage = pageobj.invokePageLoaded(actionValue);
								};
								metrics.checkpoint("&larr; Done running invokePageLoaded()");

								// Invoke event handlers
								waterMark = Sys.timestamp();
							
								if( potentialPage isa void ) {
									string uieventcomponent = (parameters.keyExists('uieventcomponent') ? parameters['uieventcomponent'] : '');
									string uieventdata = (parameters.keyExists('uieventdata') ? parameters['uieventdata'] : '');
									string uieventextra = (parameters.keyExists('uieventextra') ? parameters['uieventextra'] : '');
									
									actionObject = Cache.display.objectForID( uieventcomponent );
									if( actionObject != null )
										actionObject.setupEvent( uieventdata );
								
									if( actionObject or GUIKit.isBackgroundEvent ) {
										if( GUIKit.isBackgroundEvent ) {
											array returnJSON = [];
											monitor {
												void request_result;
												string request_id = '';
												if( actionObject ) {
													request_id = actionObject.id;
													request_result = actionObject.handleEvent( uieventdata, uieventextra );
													if( request_result ) {
														returnJSON[] = HTML.MCAMChannel('Result', request_id, request_result);
													}
												} else {
													request_id = commands[1];
													returnJSON[] = pageobj.handleMCAMRequest( request_id, parameters );
												}
											} handle ( error ) {
												returnJSON = [];
												returnJSON[] = HTML.MCAMChannel('Error', "${err.str + ', backtrace: ' + error.backtrace()}");
											} else {
												Cache.components.each() using ( component ) {
													if( component.lastChange >= waterMark ) {
														string render = component.renderForMCAM();
														if( render ) {
															returnJSON[] = render;
														}
													}
												};
											}
											// Send back the information
											Request.current().contentType('text/json; charset=UTF-8');
											return HTML.MCAMEnvelope(returnJSON);
										} else {
											metrics.checkpoint("&rarr; Running Event Handler");
											potentialPage = actionObject.handleEvent( uieventdata, uieventextra );
											metrics.checkpoint("&larr; Running Event Handler");
										}
									} else { 
										// We have no action
										metrics.checkpoint("&rarr; Running Default Handler");
										potentialPage = pageobj.defaultAction( uieventcomponent, uieventdata, uieventextra );
										metrics.checkpoint("&larr; Running Default Handler");
									}
								}
								if( not (potentialPage isa void) ) {
									return '' + potentialPage;
								}
							} else {
								return command_return;
							}
						} else {
							debug("VALIDATORS FAILED!");
						}
					} else {
						if( not (command_return = pageobj.handleApplicationCommands()) ) {
							wf_profile("pageLoaded:no_event") using {
								potentialPage = pageobj.invokePageLoaded(actionValue);
							};
							if( not (potentialPage isa void) ) {
								return '' + potentialPage;
							}
						} else {
							return command_return;
						}
					}
				} else {
					pageobj.firstLoad = true;
					if( not (command_return = pageobj.handleApplicationCommands()) ) {
						wf_profile("pageLoaded:first_load") using {
							potentialPage = pageobj.invokePageLoaded(actionValue);
						};
						if( not (potentialPage isa void) ) {
							return '' + potentialPage;
						}
					} else {
						return command_return;
					}
				}
				wf_profile("pageFinished") using { pageobj.pageFinished(); };
				pageobj.publishPersistence();
				request.contentType( contenttype );
				metrics.checkpoint("Cache loading of page");
				return Cache.loadPage( tpath );
			}
			Sys.error( "Unable to obtain page class for $page.", 0 );
		}
		
		function bindComponents() {
			array unknownComponents;
			void __PageKlass__ = self.getClass();
			object wrapper = new Object(self);
			
			log('Page.bindComponents', 'Components: ' + Cache.components.keys());
			// Bind the components
			metrics.checkpoint("Setting up object bindings");
			Array.each( __PageKlass__.linkedObjects() ) using ( key ) {
				if( Cache.components.keyExists(key) ) {
					object component = Cache.components[key];
					wrapper.setVariable(key, component);
				} else {
					unknownComponents[] = key;
				}
			};
			if( unknownComponents.size() > 0 ) {
				raise new Error( "Request to bind the components ${Array.join(unknownComponents,', ')} failed! (Do they exist within the Page file ?)" );
			}
		}
		function publishPersistence() {
			object wrapper = new Object(self);
			array  persistentValues;
			persistentValues = [];
			Array.each( self.getClass().persistentValues() ) using ( string variable ) {
				metrics.checkpoint("Storing $variable: ${wrapper.getVariable(variable)}");
				persistentValues[variable] = wrapper.getVariable(variable);
			};
			Cache.persistence = persistentValues;
		}

		function setCommandParameters( string action, array commands, array parameters ) {
			.action = action;
			.commands = commands;
			.parameters = parameters;
		}
		function defaultAction( string origin, string event, string extra );

		function registerValidator( object value ) {
			.validators[] = value;
		}
		function handleRPCRequest( string name, array parameters ) {
			return SOAP.fault('env:Sender', 'rpc:ProcedureNotPresent', 'Procedure Not Present');
		}
		function handleMCAMRequest( string name, array parameters ) {
			raise new Error('Unable to find MCAM request handler for ' + name);
		}
		function handleApplicationCommands {
			return false;
		}
		function processValidators() {
			number valid = true;	
			.validators.each() using ( validator ) {
				if( not validator.validate() )
					valid = false;
			};
			.valid = valid;
			return .valid;
		}
		function setRenderEngine( object r )
			.renderEngine = r;

		function renderEngine()
			return .renderEngine;

		function useTemplate( string t )
			.renderEngine.loadTemplate( t );

		static function getTemplateName()
			return '';

		static function getContentType()
			return 'text/html; charset=UTF-8';

		static function getWrapperTemplateName() 
			return 'Default';

		function generatedPage()
			return .renderEngine.renderWithPage( self );

		function initialiseComponents()
			return false;
		
		function invokePageLoaded( void data ) {
			.setupFeatureSystem();
			if( GUIKit.isBackgroundEvent ) {
				return .backgroundPageLoaded(data);
			} else {
				return .pageLoaded(data);
			}
		}
		void pushData;
		function backgroundPageLoaded() ;
		function backgroundPageLoaded( void data ) 	{
			.pushData = data;
			return .backgroundPageLoaded();
		}
		function pageLoaded() ;
		function pageLoaded( void data ) {
			.pushData = data;
			return .pageLoaded();
		}
		
		function pageFinished
			;
		function objectForID( string id ) {
			return .renderEngine.objectForID( id );
		}
		function getTitle() 
			return Config.config['SiteTitle'];
		function getHeaders() {
			return 
				GUIKit.requiredCSSHeaders();
		}
		function requiredJavascript
			return [ /* 1st Party Titles */ 'Debug', 'General', 'Validator', 'Hotkey', 'MCAM', 
					 /* 3rd Party Titles */ 'jquery', 'jquery-extensions', 'JSON2', 'prototype',
					 'scriptaculous/builder', 
					 'scriptaculous/effects', 
					 'scriptaculous/dragdrop', 
					 'scriptaculous/controls', 
					 'scriptaculous/slider', 
					 'scriptaculous/sound', 
					 'scriptaculous/scriptaculous', 
					 'rangy-core', 'rangy-selectionsaverestore',
					 'Component/Component.js' ];
		function requiredJavascriptExceptions 
			return [ 'ckeditor/ckeditor_source' ];

		function getJavascriptIncludes {
			string compressed = application.virtualName + '-Compressed';
			string compressedURI = uriForJavascriptResource('Generated/' + compressed);
			
			string translation = "Generated/${application.virtualName}.translation.${I18N.desiredCatalog}";
			string translationURI = uriForJavascriptResource(translation);
			
			string headers = "<!-- Runtime generated $compressed: $compressedURI -->";
			
			if( Config.Debug or (not compressedURI) ) {
				.requiredJavascript().append(GUIKit.requiredJavascriptHeaders()).each() using ( js ) {
					string uri = uriForJavascriptResource((js - '.js'));
					if( not uri ) {
						uri = __uriFor( 'Support', js, 'Components' );
					}
					uri and headers += HTML.javascriptHeader(uri);
				};
			} else {
				headers += HTML.javascriptHeader(compressedURI);
			}

			.requiredJavascriptExceptions().each() using ( js ) {
				string uri = uriForJavascriptResource((js - '.js'));
				if( not uri ) {
					uri = __uriFor( 'Support', js, 'Components' );
				}
				uri and headers += HTML.javascriptHeader(uri);
			};

			if( translationURI ) {
				headers += HTML.javascriptHeader(translationURI);
				headers += HTML.javascriptChunk("WFI18NCatalog = '${I18N.desiredCatalog}';");
			}
			
			return headers;
		}
		function getRequiredJavascript {
			return .requiredJavascript().append(GUIKit.requiredJavascriptHeaders());
		}
		function setupFeatureSystem
			;
		function actionAccessAllowed( string url )
			return true;
			
		static function DOCTYPE_header() {
			return
			"<!DOCTYPE html PUBLIC \"-//W3C//DTD XHTML 1.0 Transitional//EN\"
				\"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd\"
				[
					<!ENTITY % HTMLsymbol PUBLIC
					    \"-//W3C//ENTITIES Symbols for XHTML//EN\"
					    \"${__pathForResource('DTD/xhtml-symbol.ent','Templates')}\">
					 %HTMLsymbol;
					<!ENTITY % HTMLspecial PUBLIC
				        \"-//W3C//ENTITIES Special for XHTML//EN\"
				        \"${__pathForResource('DTD/xhtml-special.ent','Templates')}\">
				     %HTMLspecial;
				    <!ENTITY % HTMLlat1 PUBLIC
				       \"-//W3C//ENTITIES Latin 1 for XHTML//EN\"
				       \"${__pathForResource('DTD/xhtml-lat1.ent','Templates')}\">
				    %HTMLlat1;
				]
			>";
		}
   }
}
