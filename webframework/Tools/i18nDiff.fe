uses "webframework/webframework";
uses "posix";

function processString( string key ) {
	I18N.cleanString(key);
}
array master = [];
array translation = [];
array toAdd;

string applicationName = argv[0];
string language = argv[1];
string translationFile = "$applicationName/Resources/Strings/$language/translation.strings.fe";

master = include("$applicationName/Resources/Strings/Master.strings.fe");

monitor { 
	translation = include(translationFile);
} handle {
	Console.println("Unable to load translation for language $language");
}

master.keys().each() using ( key ) {
	if( not translation.keyExists(key) ) {
		Console.println( "+ Adding '$key' to translation");
		translation[key] = key;
		toAdd[key] = true;
	}
};

translation.keys().each() using ( key ) {
	if( not master.keyExists(key) ) {
		Console.println( "- Keeping '$key' from translation [even though it might have been deleted]");
	}
};

File.remove(translationFile);
File.create(translationFile) using ( file ) {
	Console.println(translationFile);
	file.writeln( 'return [' );
	file.writeln( translation.keys().sort().join(",\n") using ( key ) {
		string comment = (key == translation[key] ? ' /* Potentially Requires Translation */' : '');
		return "\t'${I18N.cleanString(key)}' => '${I18N.cleanString(translation[key])}' $comment ";
	});
	file.writeln( ']; // End of File ' );
};
